WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY) ~ NEWLINE }

Program = { SOI ~ Match* ~ EOI }

Match = { "match" ~ Expr ~ "with" ~ Case+ ~ "end" }

Case = { "|"? ~ Pattern ~ ("|" ~ Pattern)* ~ "=>" ~ Expr }

Pattern = {
    TuplePattern
    | RecordPattern
    | ArrayPattern
    | Atom
    | Id
    | "_"
}

TuplePattern = { "(" ~ (Pattern ~ ("," ~ Pattern)*)? ~ ")" }

ArrayPattern = { "[" ~ ArrayElements? ~ "]" }
ArrayElements = _{ Pattern ~ "," ~ ArrayElements | Pattern | Rest }

RecordPattern = { "{" ~ RecordElements? ~ "}" }
RecordElements = _{ Field ~ "," ~ RecordElements | Field | Rest }
Field = { "[" ~ Expr ~ "]" ~ ":" ~ Pattern | Id ~ ":" ~ Pattern | Id }

Rest = { ".." ~ Id? }

Int = @{ ASCII_DIGIT+ }
String = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
Char = @{ "'" ~ ("\\'" | !"'" ~ ANY) ~ "'"}
Bool = { "true" | "false" }
Symbol = ${ ":" ~ Id }
Id = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

Expr = { Atom | Id }

Atom = { Int | String | Char | Bool | Symbol }
